# export LANG="en_GB.UTF-8"
# export LC_COLLATE="C"
# export LC_CTYPE="UTF-8"
# export LC_MESSAGES="C"
# export LC_MONETARY="C"
# export LC_NUMERIC="C"
# export LC_TIME="C"
# export LC_ALL=$LANG

# if [ "$(arch)" = "arm64" ]; then
#     eval "$(/opt/homebrew/bin/brew shellenv)"
# else
#     eval "$(/usr/local/bin/brew shellenv)"
# fi
# export PATH="$HOMEBREW_PREFIX/opt/python/libexec/bin:$PATH"

# # p10k instant prompt
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# where should we store your Zsh plugins?
ZPLUGINDIR=$HOME/.zsh/plugins

# add your plugins to this list
plugins=(
  # core plugins
  zsh-users/zsh-autosuggestions
  zsh-users/zsh-history-substring-search
  zsh-users/zsh-completions
  zsh-users/zsh-syntax-highlighting

  # user plugins
  rupa/z
  # jeffreytse/zsh-vi-mode
  zdharma-continuum/fast-syntax-highlighting

  # prompts
  romkatv/powerlevel10k
)

if [ -n $RELOADING ]; then
  source $HOME/dotfiles/support/plugin-loader.zsh
  export PLUGINS_LOADED=true
fi

# clone, source, and add to fpath
for repo in $plugins; do
  plugin-load https://github.com/${repo}.git
done
unset repo

# https://thevaluable.dev/zsh-completion-guide-examples/
zstyle ':completion:*' file-list all # list with all info
zstyle ':completion:*' file-sort change # sort by time changed (last is first selected)
zstyle ':completion:*' menu select=4 # show selection menu

if [[ $OSTYPE =~ "darwin" ]]; then
  zstyle ':completion:*' \
         list-colors 'exfxcxdxbxegedabagacad'
  # seems to fix neonutt shortcuts
  # stty flush undef
else
  zstyle ':completion:*' \
         list-colors ${(s.:.)LS_COLORS}
fi

# load complete defs
autoload -Uz compinit
compinit

# bash complete
autoload bashcompinit
bashcompinit

# bind UP and DOWN arrow keys
zmodload zsh/terminfo
# bind k and j for VI mode
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down
# use the vi navigation keys in menu completion
bindkey -v '^n' expand-or-complete
bindkey -v '^p' reverse-menu-complete
bindkey -v '^k' history-substring-search-up
bindkey -v '^j' history-substring-search-down
bindkey -v '^h' backward-delete-char
bindkey -v '^w' backward-kill-word
bindkey -v '^e' forward-word
bindkey -v '^l' forward-char
bindkey -v '^?' backward-delete-char
# edit line
autoload -z edit-command-line
zle -N edit-command-line
bindkey "^x^e" edit-command-line
bindkey -M vicmd " " edit-command-line
# menu settings for completion
zmodload zsh/complist
bindkey -M menuselect '^xi' vi-insert
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -M menuselect 'l' vi-forward-char

#========
# ZSH OPTS
#========

# don't save duplicate commands
setopt HIST_SAVE_NO_DUPS
setopt HIST_FIND_NO_DUPS
# don't verify history expansions (can just do it with tab)
setopt NO_HIST_VERIFY
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
#append into history file
setopt INC_APPEND_HISTORY
# timestamp
setopt EXTENDED_HISTORY
setopt interactivecomments # enable interactive comments like bash

export ZVM_CURSOR_STYLE_ENABLED=false

#========
# ALIASES
#========

alias reload="RELOADING=true source ~/.zshrc"

source ~/.aliases

#========
# EXPORTS
#========

source ~/.exports
source ~/.secrets
export KEYTIMEOUT=1
if [[ $WSL ]]; then
  source ~/.wsl
fi

#========
# PROMPT
#========

[[ $TERM == *256color || $TERM == xterm* ]] || : ${PURE_POWER_MODE:=portable}
# source machine prompt if there is one, otherwise use my default
[ -f ~/.prompt.zsh ] && source ~/.prompt.zsh || source $HOME/dotfiles/support/prompt.zsh

#========
# FUNCTIONS
#========

source ~/.functions

# Allow Ctrl-z to toggle between suspend and resume
function Resume {
  fg
  zle push-input
  BUFFER=""
  zle accept-line
}
zle -N Resume
bindkey "^Z" Resume

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
[ -f ~/dotfiles/support/bindings.zsh ] && source ~/dotfiles/support/bindings.zsh
# Meta-c to Alt-c because kitty I have macos_option_as_alt no to allow symbol shortcuts
bindkey "รง" fzf-cd-widget

if type brew &>/dev/null; then
  FPATH=$(brew --prefix)/share/zsh/site-functions:$FPATH
fi

NVM_DIR="$HOME/.nvm"
if [[ -d "$NVM_DIR" ]]; then
  nvm () {
    unfunction nvm
    [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
    [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion
    nvm "$@"
  }
else
  unset NVM_DIR
fi

RBENV_ROOT="${HOME}/.rbenv"
# eval "$(rbenv init - zsh)"
# lazy load ruby env
if [[ -d "$RBENV_ROOT}" ]]; then
  rbenv () {
    if ! (($path[(Ie)${RBENV_ROOT}/bin])); then
      path[1,0]="${RBENV_ROOT}/bin"
    fi
    eval "$(rbenv init - zsh)"
    rbenv "$@"
    unfunction rbenv
  }
else
  unset RBENV_ROOT
fi

# make tmux session
# if command -v tmux &> /dev/null && [ -n "$PS1" ] && [[ ! "$TERM" =~ screen ]] && [[ ! "$TERM" =~ tmux ]] && [ -z "$TMUX" ]; then
#   exec tmux new-session -A -s main
# fi

# lazy load pyenv
PYENV_ROOT="${HOME}/.pyenv"
if [[ -d "$PYENV_ROOT}" ]]; then
  pyenv () {
    if ! (($path[(Ie)${PYENV_ROOT}/bin])); then
      path[1,0]="${PYENV_ROOT}/bin"
    fi
    eval "$(command pyenv init -)"
    pyenv "$@"
    unfunction pyenv
  }
else
  unset PYENV_ROOT
fi

# keychain for ssh agent if one not running - on macOS this will normally be the keychain integrated agent
if [ -z "${SSH_AUTH_SOCK}" ]; then
  if type keychain &>/dev/null; then
    if [[ $OSTYPE =~ "darwin" ]]; then
      eval $(keychain --nogui --eval --quiet --agents ssh --inherit any id_rsa id_ed25519)
    else
      eval $(keychain --nogui --eval --quiet id_rsa id_ed25519)
    fi
  fi
fi
