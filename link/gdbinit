set history file ~/.gdbhist
set history save on
set history remove-duplicates 1
set history expansion on

# enabled perf access
set mem inaccessible-by-default off
# set auto-load safe-path .

define xxd
  dump binary memory /tmp/dump.bin $arg0 ((char *)$arg0)+$arg1
  shell xxd /tmp/dump.bin
end
document xxd
  Runs xxd on a memory ADDR and LENGTH
  xxd ADDR LENTH
end

define skip
    tbreak +1
    jump +1
end

# lines makes more sense to me...
define lines 
  list
end

# resets a multi-image target by jump beyond MCUboot, which will fail to validate debug image
define app_reset
    # Reset the device
    monitor reset
    # Find the start address
    set $start_address = &__start
    # Set the program counter to the App start address
    set $pc = $start_address
    continue
end

python
# Update GDB's Python paths with the `sys.path` values of the local
#  Python installation, whether that is brew'ed Python, a virtualenv,
#  or another system python.

# Convert GDB to interpret in Python
import os,subprocess,sys
# Execute a Python using the user's shell and pull out the sys.path (for site-packages)
paths = subprocess.check_output('python -c "import os,sys;print(os.linesep.join(sys.path).strip())"',shell=True).decode("utf-8").split()
# Extend GDB's Python's search path
sys.path.extend(paths)

end
